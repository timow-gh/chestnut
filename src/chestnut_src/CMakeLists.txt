set(HEADER_FILES
        include/chestnut/kdtree.hpp
        include/chestnut/traits.hpp
        include/chestnut/point.hpp
        )

set(TARGET_NAME chestnut)
add_library(${TARGET_NAME} INTERFACE ${HEADER_FILES})
target_include_directories(${TARGET_NAME} INTERFACE "$<BUILD_INTERFACE:${CMAKE_CURRENT_LIST_DIR}/include>")
add_library(${TARGET_NAME}::${TARGET_NAME} ALIAS ${TARGET_NAME})

# "EXPORT_NAME: Exported name for target files.
# This sets the name for the IMPORTED target generated by the install(EXPORT) and export() commands. If not set, the
# logical target name is used by default.", CMake docs
set(TARGET_EXPORT_NAME ${TARGET_NAME})
set(${TARGET_NAME}_MAJOR 0)
set(${TARGET_NAME}_MINOR 1)
set(${TARGET_NAME}_PATCH 0)
message(STATUS "Configuring ${TARGET_NAME} ${${TARGET_NAME}_MAJOR}.${${TARGET_NAME}_MINOR}.${${TARGET_NAME}_PATCH}")

set_target_properties(${TARGET_NAME}
        PROPERTIES
        LANGUAGE CXX
        DEBUG_POSTFIX d
        VERSION 0.1.0
        SOVERSION 0
        )

include(Warnings)
add_warnings_and_compile_options(${TARGET_NAME} ${${PROJECT_NAME}_WARNINGS_AS_ERRORS})

if (${PROJECT_NAME}_STATIC_ANALYSIS)
    include(static_analysis)
    enable_static_analysis(${TARGET_NAME} ${${PROJECT_NAME}_WARNINGS_AS_ERRORS})
endif ()

install(DIRECTORY "include/${TARGET_NAME}" DESTINATION "${CMAKE_INSTALL_INCLUDEDIR}")

install(TARGETS
        ${TARGET_NAME}
        EXPORT ${TARGET_NAME}TargetsExportSet
        RUNTIME DESTINATION ${CMAKE_INSTALL_BINDIR}
        LIBRARY DESTINATION ${CMAKE_INSTALL_LIBDIR}
        ARCHIVE DESTINATION ${CMAKE_INSTALL_LIBDIR}
        INCLUDES DESTINATION ${CMAKE_INSTALL_INCLUDEDIR})

setup_export_set_installation(
        "${TARGET_NAME}"
        "${TARGET_NAME}TargetsExportSet"
        "${TARGET_NAME}Config.cmake.in"
        ${${TARGET_NAME}_MAJOR} ${${TARGET_NAME}_MINOR} ${${TARGET_NAME}_PATCH})